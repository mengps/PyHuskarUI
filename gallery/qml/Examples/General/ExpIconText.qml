import QtQuick
import QtQuick.Controls.Basic
import QtQuick.Layouts
import HuskarUI.Basic

import '../../Controls'

Item {

    HusMessage {
        id: message
        z: 999
        parent: galleryWindow.captionBar
        width: parent.width
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.top: parent.bottom
        showCloseButton: true
    }

    Flickable {
        id: flickable
        width: parent.width
        height: 400
        contentHeight: column.height
        clip: true
        ScrollBar.vertical: HusScrollBar { }

        Column {
            id: column
            width: parent.width - 15
            spacing: 30

            DocDescription {
                desc: qsTr(`
# HusIconText å›¾æ ‡æ–‡æœ¬\n
è¯­ä¹‰åŒ–çš„å›¾æ ‡æ–‡æœ¬æˆ–å›¾æ ‡ã€‚\n
* **æ¨¡å— { HuskarUI.Basic }**\n
* **ç»§æ‰¿è‡ª { [HusText](internal://HusText) }**\n
\n<br/>
\n### æ”¯æŒçš„ä»£ç†ï¼š\n
- æ— \n
\n<br/>
\n### æ”¯æŒçš„å±æ€§ï¼š\n
å±æ€§å | ç±»å‹ | é»˜è®¤å€¼ | æè¿°
------ | --- | :---: | ---
empty | bool(readonly) | - | æŒ‡ç¤ºå›¾æ ‡æ˜¯å¦ä¸ºç©º(iconSource == 0æˆ–'')
iconSource | intä¸¨string | 0ä¸¨'' | å›¾æ ‡æº(æ¥è‡ª HusIcon)æˆ–å›¾æ ‡é“¾æ¥
iconSize | int | - | å›¾æ ‡å¤§å°
colorIcon | color | - | å›¾æ ‡é¢œè‰²
contentDescription | string | '' | å†…å®¹æè¿°(æé«˜å¯ç”¨æ€§)
\n**æ³¨æ„** åŒè‰²é£æ ¼å›¾æ ‡ä½¿ç”¨éœ€è¦å¤šä¸ª<Path{1~N}>å›¾æ ‡è¦†ç›–ä½¿ç”¨\n
                           `)
            }

            ThemeToken {
                id: themeToken
                source: 'HusIconText'
                historySource: 'https://github.com/mengps/HuskarUI/blob/master/src/imports/HusIconText.qml'
            }
        }
    }

    HusDivider {
        width: parent.width
        height: 1
        anchors.bottom: flickable.bottom
    }

    HusTabView {
        anchors.top: flickable.bottom
        anchors.topMargin: 5
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        addButtonDelegate: Item {}
        tabCentered: true
        defaultTabWidth: 120
        initModel: [
            {
                key: '1',
                title: qsTr('çº¿æ¡†é£æ ¼å›¾æ ‡'),
                styleFilter: 'Outlined'
            },
            {
                key: '2',
                title: qsTr('å¡«å……é£æ ¼å›¾æ ‡'),
                styleFilter: 'Filled'
            },
            {
                key: '3',
                title: qsTr('åŒè‰²é£æ ¼å›¾æ ‡'),
                styleFilter: 'Path1,Path2,Path3,Path4'
            },
            {
                key: '4',
                title: qsTr('IcoMoonå›¾æ ‡'),
                styleFilter: 'IcoMoon'
            }
        ]
        contentDelegate: Item {
            id: contentItem

            Component.onCompleted: {
                const map = HusIcon.allIconNames();
                const filter = model.styleFilter.split(',');
                for (const key in map) {
                    let has = false;
                    filter.forEach((filterKey) => {
                                       if (key.indexOf(filterKey) !== -1) {
                                           has = true;
                                       }
                                   });
                    if (has) {
                        listModel.append({
                                             iconName: key,
                                             iconSource: map[key]
                                         });
                    }
                }
            }

            GridView {
                id: gridView
                anchors.fill: parent
                cellWidth: Math.floor(width / 8)
                cellHeight: 110
                clip: true
                model: ListModel { id: listModel }
                ScrollBar.vertical: HusScrollBar { }
                delegate: Item {
                    id: rootItem
                    width: gridView.cellWidth
                    height: gridView.cellHeight

                    required property string iconName
                    required property int iconSource

                    Rectangle {
                        anchors.fill: parent
                        anchors.margins: 10
                        color: mouseArea.pressed ? HusThemeFunctions.darker(HusTheme.Primary.colorPrimaryBorder) :
                                                  mouseArea.hovered ? HusThemeFunctions.lighter(HusTheme.Primary.colorPrimaryBorder)  :
                                                                     HusThemeFunctions.alpha(HusTheme.Primary.colorPrimaryBorder, 0);
                        radius: 5

                        Behavior on color { enabled: HusTheme.animationEnabled; ColorAnimation { duration: HusTheme.Primary.durationFast } }

                        MouseArea {
                            id: mouseArea
                            anchors.fill: parent
                            hoverEnabled: true
                            cursorShape: hovered ? Qt.PointingHandCursor : Qt.ArrowCursor
                            onEntered: hovered = true;
                            onExited: hovered = false;
                            onClicked: {
                                HusApi.setClipbordText(`HusIcon.${rootItem.iconName}`);
                                message.success(`HusIcon.${rootItem.iconName} copied ğŸ‰`);
                            }
                            property bool hovered: false
                        }

                        ColumnLayout {
                            anchors.fill: parent
                            anchors.topMargin: 10
                            anchors.bottomMargin: 10
                            spacing: 10

                            HusIconText {
                                id: icon
                                Layout.preferredWidth: 28
                                Layout.preferredHeight: 28
                                Layout.alignment: Qt.AlignHCenter
                                iconSize: 28
                                iconSource: rootItem.iconSource
                            }

                            HusText {
                                Layout.preferredWidth: parent.width - 10
                                Layout.fillHeight: true
                                Layout.alignment: Qt.AlignHCenter
                                verticalAlignment: Text.AlignVCenter
                                horizontalAlignment: Text.AlignHCenter
                                text: rootItem.iconName
                                color: icon.colorIcon
                                wrapMode: Text.WrapAnywhere
                            }
                        }
                    }
                }
            }
        }
    }
}
