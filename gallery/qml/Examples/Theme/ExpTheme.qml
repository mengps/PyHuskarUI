import QtQuick
import QtQuick.Controls.Basic
import QtQuick.Layouts
import HuskarUI.Basic

import '../../Controls'

Flickable {
    contentHeight: column.height
    ScrollBar.vertical: HusScrollBar { }

    HusMessage {
        id: message
        z: 999
        parent: galleryWindow.captionBar
        width: parent.width
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.top: parent.bottom
        showCloseButton: true
    }

    HusColorGenerator {
        id: husColorGenerator
    }

    Column {
        id: column
        width: parent.width
        spacing: 30

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.installThemePrimaryColorBase()\` æ–¹æ³•è®¾ç½®å…¨å±€ä¸»é¢˜çš„ä¸»åŸºç¡€é¢œè‰²ï¼Œä¸»åŸºç¡€é¢œè‰²å½±å“æ‰€æœ‰é¢œè‰²çš„ç”Ÿæˆã€‚
                       `)
            code: `
                HusTheme.installThemePrimaryColorBase('#ff0000');
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹ä¸»åŸºç¡€é¢œè‰²')
                }

                RowLayout {
                    spacing: 50

                    Column {
                        Layout.alignment: Qt.AlignVCenter
                        Layout.leftMargin: 50
                        spacing: 10

                        HusText {
                            padding: 10
                            text: qsTr('é¢„è®¾é¢œè‰²: ')
                        }

                        Grid {
                            columns: 5
                            spacing: 10

                            Repeater {
                                id: repeater
                                model: [
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Red), colorName: 'red' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Volcano), colorName: 'volcano' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Orange), colorName: 'orange' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Gold), colorName: 'gold' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Yellow), colorName: 'yellow' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Lime), colorName: 'lime' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Green), colorName: 'green' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Cyan), colorName: 'cyan' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Blue), colorName: 'blue' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Geekblue), colorName: 'geekblue' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Purple), colorName: 'purple' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Magenta), colorName: 'magenta' },
                                    { color: husColorGenerator.presetToColor(HusColorGenerator.Preset_Grey), colorName: 'grey' }
                                ]
                                delegate: Rectangle {
                                    id: rootItem
                                    width: 50
                                    height: 50
                                    scale: hovered ? 1.1 : 1
                                    color: hovered ? HusThemeFunctions.lighter(modelData.color, 110) : modelData.color
                                    border.color: isCurrent || hovered ? HusTheme.Primary.colorPrimaryBorderHover : 'white'
                                    radius: HusTheme.Primary.radiusPrimary

                                    property bool hovered: false
                                    property bool isCurrent: index == repeater.currentIndex

                                    Behavior on color { ColorAnimation { } }
                                    Behavior on border.color { ColorAnimation { } }
                                    Behavior on scale {
                                        NumberAnimation {
                                            easing.type: Easing.OutBack
                                            duration: HusTheme.Primary.durationSlow
                                        }
                                    }

                                    HusIconText {
                                        anchors.centerIn: parent
                                        iconSource: HusIcon.CheckOutlined
                                        iconSize: 18
                                        colorIcon: 'white'
                                        scale: visible ? 1 : 0
                                        visible: rootItem.isCurrent
                                        transformOrigin: Item.Bottom

                                        Behavior on scale {
                                            NumberAnimation {
                                                easing.type: Easing.OutBack
                                                duration: HusTheme.Primary.durationSlow
                                            }
                                        }

                                        Behavior on opacity {
                                            NumberAnimation {
                                                duration: HusTheme.Primary.durationMid
                                            }
                                        }
                                    }

                                    MouseArea {
                                        anchors.fill: parent
                                        cursorShape: Qt.PointingHandCursor
                                        hoverEnabled: true
                                        onEntered: rootItem.hovered = true;
                                        onExited: rootItem.hovered = false;
                                        onClicked: {
                                            galleryGlobal.themeIndex = repeater.currentIndex = index;
                                            HusTheme.installThemePrimaryColorBase(rootItem.color);
                                        }
                                    }
                                }
                                property int currentIndex: galleryGlobal.themeIndex
                            }
                        }

                        Row {
                            spacing: 10

                            HusText {
                                padding: 10
                                text: qsTr('è‡ªå®šä¹‰é¢œè‰²: ')
                            }

                            HusColorPicker {
                                id: customColorPicker
                                defaultValue: HusTheme.Primary.colorPrimary
                                showText: true
                                autoChange: false
                                onChange: color => selectColor = color;
                                popup.closePolicy: HusPopup.NoAutoClose
                                property color selectColor: value
                                footerDelegate: Item {
                                    height: 45

                                    HusDivider {
                                        width: parent.width - 24
                                        height: 1
                                        anchors.horizontalCenter: parent.horizontalCenter
                                    }

                                    Row {
                                        spacing: 20
                                        anchors.centerIn: parent

                                        HusButton {
                                            text: qsTr('Accept')
                                            onClicked: {
                                                customColorPicker.changeValue = customColorPicker.selectColor;
                                                customColorPicker.open = false;
                                                galleryGlobal.themeIndex = repeater.currentIndex = -1;
                                                HusTheme.installThemePrimaryColorBase(customColorPicker.selectColor);
                                            }
                                        }

                                        HusButton {
                                            text: qsTr('Cancel')
                                            onClicked: {
                                                customColorPicker.changeValue = customColorPicker.value;
                                                customColorPicker.defaultValue = customColorPicker.value;
                                                customColorPicker.open = false;
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }

                    ListView {
                        id: colorsListView
                        Layout.preferredWidth: 350
                        Layout.preferredHeight: contentHeight
                        Layout.rightMargin: 30
                        Layout.alignment: Qt.AlignVCenter
                        interactive: false
                        model: 10
                        delegate: HusRectangleInternal {
                            width: hovered ? colorsListView.width : colorsListView.width - 30
                            height: 30
                            topRightRadius: hovered ? 4 : 0
                            bottomRightRadius: hovered ? 4 : 0
                            color: HusTheme.Primary[`colorPrimaryBase-${index + 1}`]

                            property bool hovered: false
                            property string colorString: String(color).toUpperCase()

                            Behavior on width { NumberAnimation { duration: HusTheme.Primary.durationMid } }
                            Behavior on topRightRadius { NumberAnimation { duration: HusTheme.Primary.durationMid } }
                            Behavior on bottomRightRadius { NumberAnimation { duration: HusTheme.Primary.durationMid } }

                            HusText {
                                anchors.centerIn: parent
                                color: parent.color.hslLightness > 0.5 ? 'black' : 'white'
                                text: parent.colorString
                                font.bold: true
                            }

                            MouseArea {
                                anchors.fill: parent
                                cursorShape: Qt.PointingHandCursor
                                hoverEnabled: true
                                onEntered: parent.hovered = true;
                                onExited: parent.hovered = false;
                                onClicked: {
                                    HusApi.setClipbordText(parent.colorString);
                                    message.success(`${parent.colorString} copied ğŸ‰`);
                                }
                            }
                        }
                    }
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.installThemePrimaryFontSizeBase()\` æ–¹æ³•è®¾ç½®å…¨å±€ä¸»é¢˜çš„ä¸»åŸºç¡€å­—ä½“å¤§å°ï¼Œä¸»åŸºç¡€å­—ä½“å¤§å°å½±å“æ‰€æœ‰å­—ä½“å¤§å°çš„ç”Ÿæˆã€‚
                       `)
            code: `
                HusTheme.installThemePrimaryFontSizeBase(32);
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹ä¸»åŸºç¡€å­—ä½“å¤§å°')
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.installThemePrimaryFontFamiliesBase()\` æ–¹æ³•è®¾ç½®å…¨å±€ä¸»é¢˜çš„ä¸»åŸºç¡€å­—ä½“æ—å­—ç¬¦ä¸²ï¼Œè¯¥å­—ç¬¦ä¸²å¯ä»¥æ˜¯å¤šä¸ªå­—ä½“åï¼Œç”¨é€—å·åˆ†éš”ï¼Œä¸»é¢˜å¼•æ“å°†è‡ªåŠ¨é€‰æ‹©è¯¥åˆ—è¡¨ä¸­åœ¨æœ¬å¹³å°æ”¯æŒçš„å­—ä½“ã€‚
                       `)
            code: `
                HusTheme.installThemePrimaryFontFamiliesBase(''Microsoft YaHei UI', BlinkMacSystemFont, 'Segoe UI', Roboto');
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹ä¸»åŸºç¡€å­—ä½“æ—')
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.installThemePrimaryRadiusBase()\` æ–¹æ³•è®¾ç½®åœ†è§’åŠå¾„åŸºç¡€å¤§å°ã€‚
                       `)
            code: `
                HusTheme.installThemePrimaryRadiusBase(6);
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹åœ†è§’åŠå¾„åŸºç¡€å¤§å°')
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.installThemePrimaryAnimationBase()\` æ–¹æ³•è®¾ç½®åŠ¨ç”»åŸºç¡€é€Ÿåº¦ã€‚
                       `)
            code: `
                HusTheme.installThemePrimaryAnimationBase(100, 200, 300);
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹åŠ¨ç”»åŸºç¡€é€Ÿåº¦')
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.installSizeHintRatio()\` æ–¹æ³•è®¾ç½®å°ºå¯¸æç¤ºæ¯”ç‡ã€‚\n
å·²æœ‰çš„å°ºå¯¸å°†ä¼šè¦†ç›–ï¼Œæ²¡æœ‰çš„å°†æ·»åŠ ä¸ºæ–°å°ºå¯¸ã€‚\n
                       `)
            code: `
                HusTheme.installSizeHintRatio('normal', 2.0);
                HusTheme.installSizeHintRatio('veryLarge', 5.0);
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹å°ºå¯¸æç¤ºæ¯”ç‡')
                }
            }
        }

        CodeBox {
            width: parent.width
            desc: qsTr(`
é€šè¿‡ \`HusTheme.animationEnabled\` å±æ€§å¼€å¯/å…³é—­å…¨å±€åŠ¨ç”»ï¼Œå…³é—­åŠ¨ç”»èµ„æºå ç”¨æ›´ä½ã€‚
                       `)
            code: `
                HusTheme.animationEnabled = true;
            `
            exampleDelegate: Column {
                spacing: 10

                HusDivider {
                    width: parent.width
                    height: 30
                    title: qsTr('æ›´æ”¹å…¨å±€åŠ¨ç”»')
                }

                HusSwitch {
                    checked: HusTheme.animationEnabled
                    checkedText: qsTr('å¼€å¯')
                    uncheckedText: qsTr('å…³é—­')
                    onToggled: {
                        HusTheme.animationEnabled = checked;
                    }
                }
            }
        }
    }
}
